plugins {
    id 'checkstyle'
    id 'com.squareup.wire' version '4.4.0'
}

test {
    // Pass on downloader type to tests for different CI jobs. See DownloaderFactory.java and ci.yml
    if (System.properties.containsKey('downloader')) {
        systemProperty('downloader', System.getProperty('downloader'))
    }
    useJUnitPlatform()
    dependsOn checkstyleMain // run checkstyle when testing
}

checkstyle {
    getConfigDirectory().set(rootProject.file("checkstyle"))
    ignoreFailures false
    showViolations true
    toolVersion checkstyleVersion
}

wire {
    java {
    }
}

checkstyleTest {
    enabled false // do not checkstyle test files
}

dependencies {
    implementation project(':timeago-parser')

    implementation "com.github.TeamNewPipe:nanojson:$nanojsonVersion"
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation "org.java-websocket:Java-WebSocket:1.4.1"
    implementation "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"
    implementation 'org.nibor.autolink:autolink:0.10.0'
    implementation "com.google.protobuf:protobuf-java:3.11.0"
    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    implementation "org.brotli:dec:0.1.1"
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'commons-codec:commons-codec:1.16.0'
    implementation "org.json:json:20231013"
    implementation 'org.cache2k:cache2k-api:2.6.1.Final'
    implementation 'org.cache2k:cache2k-core:2.6.1.Final'


    // do not upgrade to 1.7.14, since in 1.7.14 Rhino uses the `SourceVersion` class, which is not
    // available on Android (even when using desugaring), and `NoClassDefFoundError` is thrown
    implementation 'org.mozilla:rhino:1.7.13'

    checkstyle "com.puppycrawl.tools:checkstyle:$checkstyleVersion"

    testImplementation platform("org.junit:junit-bom:$junitVersion")
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'


    testImplementation 'com.google.code.gson:gson:2.9.1'
}
